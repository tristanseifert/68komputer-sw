; include our hardware defines
  include "../hardware.68k"
  include "../loader_api.68k"

;       NOTES   : PORTED TO PC BY DAVE GOODMAN
;               : PORTED TO RAYMATIC 68K BOARD BY RUSSELL G. BROWN
;       ASSEMBLE: mas68k -l vubug.68k
;               : ASSEMBLER WILL TAKE FILE vubug.68k AND
;               : WILL CREATE vubug.o, vubug.l
;       VER 3.0 : VUBUG VER 3.0 PORTEd TO PC FOR ASSEMBLE USING a68K 9/84
;       VER 3.1 : VUBUG VER 3.1 FIXED FOR VU68K BOARD 10/86
;       VER 4.0 : VUBUG VER 4.0 PORTED TO RAYMATIC 68K BOARD 4/88
;*******************************************
;*                                         *
;********         V U B U G         ********
;*                                         *
;* Copyright (C) 1983, 1988                *
;*               Vanderbilt Univ.          *
;*               Comp. Sci. dept.          *
;*               PO Box 1679               *
;*               Station B                 *
;*               Nashville, Tenn.          *
;*                          37235          *
;*                                         *
;*       author:                           *
;*              Edward M. Carter           *
;*                                         *
;*******************************************
;*                                         *
;* This software was prepared for distri-  *
;* bution by Russell G. Brown.  Please     *
;* note that it is being distributed at    *
;* the request of several readers of the   *
;* usenet newsgroup comp.sys.m68k.  It has *
;* no warranty of any sort, at all.  I     *
;* think that the code is fairly self      *
;* explanatory.  Good luck in modifying    *
;* it to work on whatever application you  *
;* may have.                               *
;*******************************************
;*                                         *
;* although the information contained here-*
;* in, as well as any information provided *
;* relative thereto, has been carefully re-*
;* viewed and is believed correct, Vander- *
;* bilt University assumes no liability    *
;* arising out of its application or use,  *
;* neither does it convey any license under*
;* its patent rights nor the rights of     *
;* others.                                 *
;*                                         *
;*******************************************
;*                                         *
;* The commands supported are as follows:  *
;*                                         *
;*         m - examine/update memory       *
;*         l - load program from host      *
;*         u - load program from terminal  *
;*         d - display blocks of memory    *
;*         t - single-step a program       *
;*         s - single-step                 *
;*         g - start a user mode program   *
;*         <cr> - short g command          *
;*         b - set/remove breakpoints      *
;*         r - examine/update registers    *
;*         p - prototype commands          *
;*         e - terminal emulator mode      *
;*         c - copy memory blocks          *
;*         x - print help messages         *
;*                                         *
;* Sub-command under each of these commands*
;* are shown in the source code for each   *
;* command.                                *
;*                                         *
;*******************************************
;
; Queue structure
;
        org     $0
head    =       $0000
tail    =       $0002
count   =       $0004
queue   =       $0006
;
; Breakpoint structure
;
        org     $0
instr   =       $0000
iloc    =       $0002
;
; The following is the exception vector
; table for the monitor. There should be
; no further "org"s to address contained
; herein as this will destroy the vector
; for iterrupts, breakpoints...
;
        org     $0                      ;Reset Vector
        dc.l    stack                   ;system stack
        dc.l    start                   ;initial pc
        dc.l    abhlr                   ;bus error
        dc.l    abhlr                   ;address error
        dc.l    bhlr                    ;illegal instruction vector
        dc.l    ghlr                    ;zero divide
        dc.l    ghlr                    ;chk
        dc.l    ghlr                    ;trapv
        dc.l    phlr                    ;privileged instruction trap
        dc.l    thlr                    ;trace handler
        dc.l    bhlr                    ;emulator trap 1010
        dc.l    bhlr                    ;emulator trap 1111
        org     $3c
        dc.l    ghlr                    ;uninitialized iterrupt
        org     $60

        dc.l    IRQ_Unused                    ;spurious iterrupt
        dc.l    IRQ_Unused                    ; irq1: unused
        dc.l    VUBUG_MC68681_ISR       ; irq2: DUART
        dc.l    IRQ_Unused                    ;user auto-vectors
        dc.l    IRQ_Unused
        dc.l    IRQ_Unused
        dc.l    IRQ_Unused
        dc.l    IRQ_Unused

        dc.l    texit                   ;exit
        dc.l    tgetb                   ;getb
        dc.l    tgetw                   ;getw
        dc.l    tgetl                   ;getl
        dc.l    twrtb                   ;wrtb
        dc.l    twrtw                   ;wrtw
        dc.l    twrtl                   ;wrtl
        dc.l    tgetc                   ;getc
        dc.l    twrts                   ;wrts
        dc.l    twrtc                   ;wrtc
        dc.l    tcrlf                   ;crlf
utrpb:  dc.l    utrapb                  ;user trap vectors
utrpc:  dc.l    utrapc
utrpd:  dc.l    utrapd
utrpe:  dc.l    utrape
utrpf:  dc.l    utrapf

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
;************************************
;*                                  *
;*       Monitor data area          *
;*                                  *
;************************************
;
; RAM Base address
        org     RAM_Monitor


utrapb: dc.l    $00000000
utrapc: dc.l    $00000000
utrapd: dc.l    $00000000
utrape: dc.l    $00000000
utrapf: dc.l    $00000000

userv1: dc.l    $00000000
userv2: dc.l    $00000000
userv3: dc.l    $00000000
userv4: dc.l    $00000000
userv5: dc.l    $00000000

uav3:   dc.l    $00000000
uav4:   dc.l    $00000000
uav5:   dc.l    $00000000
uav6:   dc.l    $00000000
uav7:   dc.l    $00000000

sarea:  dcb.w   $80,$0000               ;system stack
stack   =       *
bktab:  dcb.w   $0a,$0000               ;breakpoint table

_sr:    dc.w    $0000                   ;register save area
_pc:    dc.l    $00000000

dr0:    dc.l    $00000000
dr1:    dc.l    $00000000
dr2:    dc.l    $00000000
dr3:    dc.l    $00000000
dr4:    dc.l    $00000000
dr5:    dc.l    $00000000
dr6:    dc.l    $00000000
dr7:    dc.l    $00000000
ar0:    dc.l    $00000000
ar1:    dc.l    $00000000
ar2:    dc.l    $00000000
ar3:    dc.l    $00000000
ar4:    dc.l    $00000000
ar5:    dc.l    $00000000
ar6:    dc.l    $00000000
ar7:    dc.l    $00000000

ptab:   dcb.w   $3,0000                 ;prototype table
rexam:  dc.w    $0000                   ;register examination pointer
exam:   dc.w    $0000                   ;memory examination pointer

t1:     dc.w    $0000                   ;temporary work area
t2:     dc.w    $0000
t3:     dc.w    $0000
t4:     dc.w    $0000
t5:     dc.w    $0000

ctrls:  dc.w    $0000                   ;ctrl-s,ctrl-q flag
bkptf   =       *-1                     ;breakpoint flag

; lbuff:  dcb.w   $0b,0000                ;load buffer
; ibuff:  dcb.w   $0b,0000                ;terminal buffer
users:  dcb.w   $80,0000                ;users stack area
ustck   =       *

aend:   dc.w    $0000
siz:    dc.w    $0000
writm:  dc.w    $0000
echo:   dc.w    $0000                   ;terminal port echo flag

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; queues of received characters. the oldest character will always be at address
; 0 of the queue, with newly received characters being placed at successive
; locations. A counter is provided to indicate the locaton at which data is
; currently being written.
;
; Detecting whether data is available in the queue can be done by checking the
; write pointer: if it is at the head of the queue (0) there is no data.
;
; Queues are represented as follows:
; - $00: Write pointer
; - $01: Unused, should be 0
; - $02: Data area, 16 bytes
UART_Queue_WritePtr             = $0
UART_Queue_Data                 = $2

UART_Queue_DataLen              = $10

UART_A_Queue:
  dc.b  0, 0
  dc.l  0, 0, 0, 0
  even

UART_B_Queue:
  dc.b  0, 0
  dc.l 0, 0, 0, 0
  even

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;+++++++++++++++++++++++++++++++++++++++++
;                                        +
;            Start of monitor            +
;                                        +
;+++++++++++++++++++++++++++++++++++++++++
        org     $C0

; this is the escape sequence Esc E
csc     =       $1b45           ;clear screen (h-19)
; define some other characters
cr      =       $0d
lf      =       $0a
nul     =       $00

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
howdy:  dc.w    csc
        dc.b  "M68000 Monitor VUBUG "
        dc.b "Version 4.0 11 August, 1988"
rnn0:   dc.b    cr,lf,lf,nul

; bad command entered
Msg_InvalidCommand:
  dc.b  ": Bad Command"
  dc.b    cr, lf, nul

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; These are seven user interrupt vectors
; which must remain at address $0100.
; Please ensure that they do
;
vect1:  dc.l    userv1
vect2:  dc.l    userv2
vect3:  dc.l    userv3
vect4:  dc.l    userv4
vect5:  dc.l    userv5

ssri:   dc.w    $2000           ;supervisor SR
ssrn:   dc.w    $2700           ;same w/o interrupts

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; prompt to accept input at
Msg_Prompt:
  dc.b    cr,lf
  dc.b    '!'
  dc.b    nul

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; monitor entry
start:
        ; set up the UARTs
        move    #$2700, sr
        bsr     MC68681_Reset

        ; RXRDY_B, break_A, RXRDY_A; then install our IRQ handler
        move.b  #%00100110, (MC68681_Base+MC68681_W_IMR)

        move.l  #VUBUG_MC68681_ISR, (Loader_IRQ2)

        ; restore interrupts
        move    #$2000, sr
; memory test
;         move.w  #$ffff,d0               ;load $ffff for first memory test
;         movea.w #$4000,a0               ;start at $4000
;         move.w  #$1fff,d2               ;$2000 words of ram
; mt1:    move.w  d0,(a0)                 ;store value
;         move.w  (a0)+,d1                ;load it back
;         cmp.w   d0,d1
;         bne     memerr                  ;if not the same, memory error.
;         dbf     d2,mt1
;         move.w  #$0000,d0               ;load $0000 for second mem test
;         movea.w #$4000,a0
;         move.w  #$1fff,d2
; mt2:    move.w  d0,(a0)
;         move.w  (a0)+,d1
;         bne     memerr                  ;if not zero, memory error.
;         dbf     d2,mt2
;         bra     gwan

; get here if memory test failed
; memerr: lea     memnot,a0               ;RAM doesn't work right.
;         bsr     writs
;         bra     gwan
;
; memnot: dc.b    "RAM ERROR!!!!"
;         dc.b    cr,lf,nul

; set up buffers
gwan:
        ; clear both of our receive buffers
        moveq   #0, d0
        move.b  d0, (UART_Queue_WritePtr+UART_A_Queue)
        move.b  d0, (UART_Queue_WritePtr+UART_B_Queue)

        lea     UART_Queue_Data+UART_A_Queue, a0
        lea     UART_Queue_Data+UART_B_Queue, a1
        move    #3, d1

.Clear:
        move.l  d0, (a0)+
        move.l  d0, (a1)+
        dbf     d1, .Clear

        ; lea     ibuff,a1                ;setup buffers
        ; move.w  #$0000,head(a1)
        ; move.w  #$ffff,tail(a1)
        ; move.w  #$0000,count(a1)
        ; lea     lbuff,a1
        ; move.w  #$0,head(a1)            ;set queue for lp
        ; move.w  #$ffff,tail(a1)
        ; move.w  #0,count(a1)

        move.w  #0,exam                 ;initialize control variables
        move.b  #0,ctrls
        move.b  #0,bkptf
        moveq   #$4,d0                  ;clear breakpoint table
        lea     bktab,a1
slp:    move.l  #0,(a1)+
        dbf     d0,slp
        move.w  #0,_sr                  ;clear register save area
        move.l  #$10,d0
        lea     _pc,a0
slp1:   move.l  #0,(a0)+
        dbf     d0,slp1
        lea     ustck,a0                ;set user stack pointer
        move.l  a0,ar7
        lea     sarea,a0                ;clear system stack
        moveq   #$3f,d0
slp2:   move.l  #0,(a0)+
        dbf     d0,slp2

done:
  ; setup is done. enable IRQs, and output the header/version
  move.w  ssri,sr                 ;enable interrupts at CPU
  lea     howdy,a0                ;say hello
  bsr     writs

  ; enable receiving characters here (TODO)

  bset    #$00,echo               ;turn on terminal echo
  bra     comm                    ;enter command loop

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; Generalized write facility writes 1 byte
; passed in d0 to tty. (UART A)
;
writ:
  btst    #0, ctrls               ; Is ctrl-s active
  beq     .doWrite                ; no, so write character

  stop    #$2000                  ; yes; wait until next int
  bra     writ                    ; when awakened check again

.doWrite:
  ; write character to DUART
  move.b  d0,(MC68681_Base+MC68681_W_THRA)

  ; then, wait for tx to complete
.wait:
  ; read the status register, check TXRDY bit (2), loop while clear
  move.b  (MC68681_Base+MC68681_R_SRA), d0
  btst    #2, d0
  beq     .wait
  rts

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; Generalized write facility writes 1 byte
; passed in d0 to serial port (UART B)
;
writu:
  ; write character to DUART
  move.b  d0,(MC68681_Base+MC68681_W_THRB)

  ; then, wait for tx to complete
.wait:
  move.b  (MC68681_Base+MC68681_R_SRB), d0
  btst    #2, d0
  beq     .wait
  rts

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; Generalized routine to write a string which
; must terminate in a null (goes to UART A/tty)
;
writs:
  ; get character of string, exit if null
  move.b  (a0)+, d0                ;a0 is address of string
  beq     .end

  ; write the char and check the next one
  bsr     writ
  bra     writs

.end:
  rts

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
;Generalized routine to write
;a word, byte or long word. (goes to UART A/tty)
;
writb:
  move.w  #$1, t1                  ;t1 is the number of bytes
  bra     wr

writw:
  move.w  #$3, t1
  bra     wr

writl:
  move.w  #$7, t1
  ; fall through to wr


wr:
  movem.l d1-d2/a0/a6, -(sp)

  ; get the count set above, null terminate the string and set it as a stack
  move.w  (t1), d2

  lea     t5+1, a6
  move.b  #0, (a6)

alp:
  ; convert each hex digit to an ASCII byte
  move.b  d0, d1
  and.b   #$0f, d1

  ; is it A-F?
  cmp.b   #$0a, d1
  blt     or3

  ; yeah, format it as such
  or.b    #$40, d1
  sub.b   #$09, d1
  bra     m1

  ; handle it being a number
or3:
  or.b    #$30, d1

  ; push that ASCII character onto our "string stack"
m1:
  move.b  d1, -(a6)
  lsr.l   #4, d0

  ; continue til we're out of nybbles
  dbf     d2, alp

  ; then, write that string to the terminal
  movea.w a6, a0
  bsr     writs

  ; restore regs
  movem.l (sp)+, d1-d2/a0/a6
  rts

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; include any of our hardware glue
        include "hw_glue.68k"

        dc.b    "Kushblunt, tasty!"
        even

; unused IRQ
IRQ_Unused:
  rte

; IRQ handler that forwards to RAM
IRQ_Lvl2:
  move.l  a0, -(sp)
  lea     (Loader_IRQ2), a0
  jsr     (a0)
  move.l  (sp)+, a0
  rte

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; XXX: this would be the "TTY"/UART A read character interrupt
; TODO: make this work
;
; Interrupt handler for the keyboard
; interrupt. It simply stashes the input
; character in a buffer pointed to by a1.
;
; inint:  move.w  ssrn,sr                 ;disable interrupts
;         movem.l #$e060,-(a7)            ;save registers d0,d1,d2,a1,a2
;         move.b  ttyd,d1                 ;get the character
;         andi.b  #$7f,d1                 ;mask out the parity, if any
;         btst    #$3,bkptf               ;In emulator mode ?
;         beq     incmp                   ;No, so continue
;
;         ; everything below is for terminal emulator move til incmp
;         cmp.b   #$0c,d1                 ;ctrl-l ?
;         beq     inld                    ;yes, so load
;         cmp.b   #$18,d1                 ;No, is it a ctrl-x ?
;         bne     inwru                   ;No, so write it
; inld:   bclr    #$3,bkptf               ;Exit emulator mode
;         lea     emudn,a1                ;set up return
;         move.l  a1,$16(a7)              ;put return deep in stack
;         cmp.b   #$0c,d1                 ;ctrl-l ?
;         bne     out                     ;No
;         bra     incmp                   ;check for ctrl-s or q
; inwru:  move.b  d1,d0                   ;write to host
;         bsr     writu
;         bra     out
;
;         ; check for some control characters
; incmp:  cmp.b   #$03,d1                 ;check for ctrl-c
;         beq     rstrt
;         cmp.b   #$13,d1                 ;check for ctrl-s
;         bne     ctrlq
;
;         ; yes, ctrl+S is set; exit after
;         move.b  #$1,ctrls
;         bra     out
;
; ctrlq:  cmp.b   #$11,d1                 ;check for ctrl-q
;         bne     c1
;
;         ; yes, ctrl+q is set; exit after
;         move.b  #$0,ctrls
;         bra     out
;
;         ; try to stuff it into the queue
; c1:     lea     ibuff,a1                ;get buffer address
;         cmpi.b  #$10,count(a1)          ;overflow ?
;         blt     cont                    ;No
;         bra     out                     ;Yes, so ignore character
; cont:   addq.b  #$1,tail(a1)            ;add chacter to buffer
;         addq.b  #$1,count(a1)           ;add 1 to count
;         andi.b  #$0f,tail(a1)           ;modulo-16
;         move.w  tail(a1),d2             ;get offset of new entry
;         ror.w   #$08,d2                 ;in lower 8 bits of d2
;         andi.w  #$ff,d2                 ;mask off what's left
;         lea     queue(a1),a1            ;get address of queue
;         move.b  d1,$0(a1,d2.w)          ;move byte into buffer
;
;         btst    #$00,echo               ;is the echo turned on?
;         beq     out                     ;if not, skip the echo,
;         move.b  d1,d0                   ;else setup for echo
;         bsr     writ
; out:    movem.l (a7)+,#$0607            ;reset registers d0,d1,a1,a2
;         rte

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; XXX: this would be the serial/UART B read character interrupt
; TODO: make this work
;
; Interrupt handler for the load port.
; It stashes the character in the buffer
; pointed to by a1.
;
; lpint:  move.w  ssrn,sr                 ;same as above but less complex
;         movem.l #$e060,-(a7)            ;registers d0,d1,d2,a1,a2
;         move.b  lpd,d1                  ; read char
;         andi.b  #$7f,d1                 ; this masks the parity bit
;         btst    #$3,bkptf               ; are we in terminal emulator mode
;         beq     lplea                   ; nope, so exit
;         move.b  d1,d0                   ; echo for emulator mode
;         bsr     writ
;         bra     lout
;
; lplea:  lea     lbuff,a1                ;queue a character
;         ; is there space in the queue?
;         cmpi.b  #$10,count(a1)
;         blt     lcont
;         ; nah, drop the character
;         bra     lout
;
;         ; shove char into queue
; lcont:  addq.b  #$1,tail(a1)
;         addq.b  #$1,count(a1)
;         andi.b  #$0f,tail(a1)
;         move.w  tail(a1),d2
;         ror.w   #$08,d2
;         andi.w  #$ff,d2
;         lea     queue(a1),a1
;         move.b  d1,$0(a1,d2.w)
;
; lout:   movem.l (a7)+,#$0607            ;regs d0,d1,d2,a1,a2
;         rte


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; Rstrt - restart from control c
;
VUBUG_Restart:
    ; restore all the registers we backed up earlier
    movem.l (sp)+, d0-d1/a0 ; from VUBUG_MC68681_ISR

    ; build a "fake" exception return frame
    movem.l #$ffff,dr0              ;save registers d0-a7
    move.l  usp,a0                  ;save user stack pointer
    move.l  a0,ar7
    move.w  (a7)+,_sr               ;save status register from stack
    move.l  (a7)+,_pc               ;save program counter from stack
    pea     comm                    ;fake a return to command loop
    move.w  ssri,-(a7)              ;fake a new status register
    rte

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; CRLF - write a carriage return and
; line feed. (to TTY/UART A)
;
crlf:
  lea     .NewlineData, a0
  bsr     writs
  rts

.NewlineData:
  dc.b    cr, lf, nul, nul
  even

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; Generalized number fetcher (place in a0 the routine to use for reading)
;
getb:
  move.w  #1, t1
  bra     gb

getw:
  move.w  #3, t1
  bra     gb

getl:
  move.w  #7, t1


gb:
  ; save registers, and restore our counter
  movem.l d1-d2, -(a7)
  move.w  t1, d2
  moveq   #0, d1

blp:
  ; call the input routine
  jsr     (a0)

  ; is it A-F?
  cmp.b   #$3a, d0
  blt     n1
  add.b   #$09, d0

n1:
  and.b   #$0f, d0
  asl.l   #$4, d1                  ;place in next hex didgit
  or.b    d0, d1
  dbf     d2, blp
  move.l  d1, d0                   ;setup return in d0

  ; restore registers
  movem.l (a7)+, d1-d2
  rts



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; command interpreter. each entry is 6 bytes
;
CommandTable:
  dc.w    $4d00           ;m - memory update
  dc.l    mem
  dc.w    $4c00           ;l - load from host (S-format)
  dc.l    load
  dc.w    $0c00           ;ctrl-l - load from host (S-format)
  dc.l    lnoof
  dc.w    $5500           ;u - upload program from terminal port
  dc.l    uload
  dc.w    $4400           ;d - dump contents of memory
  dc.l    dump
  dc.w    $5300           ;s - single step
  dc.l    singl
  dc.w    $5400           ;t - trace a program's path
  dc.l    trace
  dc.w    $4700           ;g - start user program
  dc.l    go
  dc.w    $0d00           ;<cr> - short g command
  dc.l    ggo
  ; dc.w    $4500           ;e - enter terminal emulator mode
  ; dc.l    emul
  dc.w    $4200           ;b - set/remove breakpoints
  dc.l    bkpt
  dc.w    $4300           ;c - copy memory blocks
  dc.l    copy
  dc.w    $5200           ;r - display/modify registers
  dc.l    regs
  dc.w    $5000           ;p - prototype commands
  dc.l    proto
  dc.w    $4800           ;h - display help messages
  dc.l    help
  dc.w    $4100           ;a - assembly listing
  dc.l    assem
CommandTable_End:

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; Structure of each entry is command
; (com) and address of servicing routine
; (code) (XXX: this will break horribly if we're mapped above $00FFFF)
;
com     =       $0
code    =       $2

comm:
  ; display command prompt
  lea     Msg_Prompt, a0
  bsr     writs

  ; read a character in, uppercase it, and begin searching the command table
  bsr     getch
  and.b   #$5f, d0

  lea     CommandTable-6, a2
  ; moveq   #$0e, d2
  moveq   #((CommandTable_End-CommandTable)/6)-1, d2

clp:
  ; advance to the next slot
  addq.w  #6, a2

  ; does the character match?
  cmp.b   com(a2), d0
  ; if not, check the next slot
  dbeq    d2, clp
  ; if it didn't match, we're at the end and couldn't find a command
  bne     .BadCommand

  ; get the address of the routine and jump to it
  movea.l code(a2), a2
  jsr     (a2)
  bra     comm


.BadCommand:
  ; print a "bad command" message
  lea     Msg_InvalidCommand, a0
  bsr     writs
  bra     comm

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; memory related commands
  include "cmd_mem.68k"

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; Regs - Modify/examine registers
;
;***************************************
;*                                     *
;* Register subcommands are as follows:*
;*                                     *
;*      r<cr> - start register mode    *
;*      r xx - start r mode at register*
;*        xx. Where xx :               *
;*            SR/_sr - status register *
;*            PC/_pc - program counter *
;*            d0 - d7 - data registers *
;*            a0 - a7 - address regs.  *
;*     .xx - set pointer to regsister  *
;*     =xxxxxxxx - update current reg- *
;*                 ister to xxxxxxxx   *
;*     <cr> - print all registers      *
;*                                     *
;***************************************
;
rtab:   dc.w    $2e00           ;.
        dc.w    rdot
        dc.w    $3d00           ;=
        dc.w    requ
        dc.w    $0d00           ;<cr>
        dc.w    rall
rtab1:  dc.w    $cb00           ;internal name/offset
        dc.b    "SR"                    ;print name
        dc.w    $9c02
        dc.b    "PC"
        dc.w    $d006
        dc.b    "d0"
        dc.w    $d10a
        dc.b    "d1"
        dc.w    $d20e
        dc.b    "d2"
        dc.w    $d312
        dc.b    "d3"
        dc.w    $d416
        dc.b    "d4"
        dc.w    $d51a
        dc.b    "d5"
        dc.w    $d61e
        dc.b    "d6"
        dc.w    $d722
        dc.b    "d7"
        dc.w    $a026
        dc.b    "a0"
        dc.w    $a12a
        dc.b    "a1"
        dc.w    $a22e
        dc.b    "a2"
        dc.w    $a332
        dc.b    "a3"
        dc.w    $a436
        dc.b    "a4"
        dc.w    $a53a
        dc.b    "a5"
        dc.w    $a63e
        dc.b    "a6"
        dc.w    $a742
        dc.b    "a7"
rmes:   dc.b    lf,lf,cr
        dc.b    "Register Mode", nul
rprmp:  dc.b    lf,cr,':',nul
reqm:   dc.b    " == ", nul
regs:   bsr getch                       ;get delimiter
        cmp.b   #$0d,d0                 ;if <cr> then start at sr
        beq     rnoad
        lea     getch,a0                ;else set for terminal input
        bsr     getb                    ;get register name
        bsr     raddr                   ;set address
        bra     rplp                    ;set register pointer
rnoad:  lea     rtab1,a3                ;set default pointer value
        move.w  a3,rexam
rplp:   lea     rmes,a0                 ;say hello
        bsr     writs
        bsr     rloc                    ;write starting loc value
rlp:    lea     rprmp,a0                ;write register prompt
        bsr     writs
        bsr     getch                   ;get command
        moveq   #$2,d2                  ;set for search
        lea     rtab-4,a0
rmlp:   addq.w  #$4,a0                  ;search
        cmp.b   (a0),d0
        dbeq    d2,rmlp
        bne     rexit                   ;exit if not found
        movea.w $2(a0),a0               ;found it so go to it
        jsr     (a0)
        bra     rlp                     ;go again
rexit:  rts
rdot:   lea     getch,a0                ;set register pointer
        bsr     getb
        bsr     raddr                   ;set input address
        bsr     rloc                    ;write register and value
        rts
requ:   lea     getch,a0                ;set new value
        movea.w rexam,a3
        moveq   #$0,d1                  ;clear d1
        move.b  $1(a3),d1               ;get offset
        beq     requs                   ;branch if sr is reg
        bsr     getl                    ;get new value
        lea     _sr,a4                  ;find save area offset
        adda.l  d1,a4                   ;add offset
        move.l  d0,(a4)                 ;move in new value
        bra     requr                   ;print it
requs:  bsr     getw                    ;same as above but for sr (word vs. long
        move.w  d0,_sr
requr:  bsr     rloc                    ;write new value
        rts
rall:   lea     rtab1-4,a3              ;write all registers
        moveq   #$11,d2                 ;set count
ralp:   addq.w  #$4,a3                  ;loop
        move.w  a3,rexam
        bsr     rloc
        dbf     d2,ralp
        rts
raddr:  move.w  #$11,d4                 ;find offset in save area
        lea     rtab1-4,a3
radlp:  addq.w  #$4,a3
        cmp.b   (a3),d0
        dbeq    d4,radlp
        bne     rexit
        move.w  a3,rexam                ;set register pointer
        rts
rloc:   bsr     crlf                    ;print register name and value
        movea.w rexam,a4
        move.b  $2(a4),d0               ;write name
        bsr     writ
        move.b  $3(a4),d0
        bsr     writ
        lea     reqm,a0
        bsr     writs
        moveq   #$0,d0
        move.b  $1(a4),d0               ;write value
        beq     rpsr                    ;branch if sr
        lea     _sr,a0                  ;find offset
        adda.l  d0,a0                   ;add offset
        move.l  (a0),d0                 ;move in new value
        bsr     writl
        bra     rrts
rpsr:   move.w  _sr,d0                  ;write sr value
        bsr     writw
rrts:   rts

; load commands
  include "cmd_load.68k"
; various debug commands
  include "cmd_debug.68k"
; include interrupt handlers
  include "irq_handlers.68k"
; help and assembly listing
  include "cmd_help.68k"
  include "cmd_listing.68k"

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; hardware routines
  include "../68681.68k"

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; stub tick handler
Loader_Handle_Tick:
  rts
